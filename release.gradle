configure(subprojects - project(':example')) {
    apply plugin: 'maven-publish'
    apply plugin: 'signing'

    task sourcesJar(type: Jar) {
        archiveClassifier.set("sources")
        from sourceSets.main.allJava
    }

    task javadocJar(type: Jar) {
        archiveClassifier.set("javadoc")
        from javadoc.destinationDir
    }

    publishing {
        publications {
            baigan(MavenPublication) {
                from components.java
                artifact sourcesJar
                artifact javadocJar
                pom {
                    name = "baigan-$project.name"
                    url = 'https://github.com/zalando-stups/baigan-config'
                    scm {
                        connection = 'scm:git:git@github.com:zalando-stups/baigan-config.git'
                        developerConnection = 'scm:git:git@github.com:zalando-stups/baigan-config.git'
                        url = 'https://github.com/zalando-stups/baigan-config'
                    }
                    licenses {
                        license {
                            name = 'The MIT License (MIT)'
                            url = 'http://opensource.org/licenses/MIT'
                        }
                    }
                }
            }
        }

        repositories {
            maven {
                name = 'local'
                url = "file://${buildDir}/local-repo"
            }
            maven {
                name = 'sonatype'
                url = 'https://oss.sonatype.org/service/local/staging/deploy/maven2/'
                credentials {
                    username = ossrhUser
                    password = ossrhPassword
                }
            }
        }
    }

    signing {
        sign publishing.publications.baigan
    }
}
